statements  : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement		: KEYWORD:RETURN expr?
						: KEYWORD:CONTINUE
						: KEYWORD:BREAK
						: expr

expr        : KEYWORD:VAR IDENTIFIER EQ expr
            : comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr   : NOT comp-expr
            : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr  :	term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            : power

power       : call (POW factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : INT|FLOAT|STRING|IDENTIFIER
            : LPAREN expr RPAREN
            : list-expr
            : if-expr
            : for-expr
            : while-expr
            : func-def

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : KEYWORD:IF LPAREN expr RPAREN LcPAREN expr RcPAREN
                                                                (KEYWORD:ELIF LPAREN expr RPAREN LcPAREN expr RcPAREN)*
                                                                (KEYWORD:ELSE LcPAREN expr RcPAREN)?



for-expr    : KEYWORD:FOR LPAREN IDENTIFIER EQ expr KEYWORD:TO expr RPAREN
              (KEYWORD:STEP expr)? LcPAREN expr RcPAREN

while-expr  : KEYWORD:WHILE LPAREN expr RPAREN LcPAREN expr RcPAREN


func-def    : KEYWORD:FUN IDENTIFIER?
              LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
              (ARROW expr)
            | (NEWLINE statements KEYWORD:END)
